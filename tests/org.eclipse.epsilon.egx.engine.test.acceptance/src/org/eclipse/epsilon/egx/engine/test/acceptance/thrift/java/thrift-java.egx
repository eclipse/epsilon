import "../thrift-helper-functions.eol";

pre {
	var fileName = getThriftFileName();	//This is typically parameterized!
	var date : String = getDate();
	doSetup();
	var extension : String = ".java";
	var javaName : String = fileName.makeValidName();
	var constsName : String = javaName+"Constants";
	var structEGL : String = "thrift-java-struct.egl";
	var pkgId : String = getNamespaceFor("java");
	var author : String = "Thrift Compiler (0.9.3)";
	var outDir : String = getOutputDir(pkgId);
}

rule Enums transform enumeration : Enum {
	parameters : Map {
		"enumeration" = enumeration,
		"kvp" = enumeration.pairs,
		"identifier" = enumeration.identifier,
		"package" = pkgId,
		"comment" = enumeration.comment,
		"date" = date,
		"author" = author
	}
	template: "thrift-java-enum.egl"
	target: outDir+enumeration.identifier+extension
}

rule Structs transform struct : Struct {
	parameters {
		struct.~isInner = false;
		struct.~isResult = false;
		var params = Map {
			"struct" = struct,
			"isException" = struct.isException,
			"isInner" = false,
			"identifier" = struct.identifier,
			"fields" = struct.fields,
			"package" = pkgId,
			"comment" = struct.comment,
			"date" = date,
			"author" = author
		};
		return params;
	}
	template: structEGL
	target: outDir+struct.identifier+extension
}

rule Unions transform union : Union {
	parameters : Map {
		"union" = union,
		"identifier" = union.identifier,
		"fields" = union.fields,
		"package" = pkgId,
		"comment" = union.comment,
		"date" = date,
		"author" = author
	}
	template: "thrift-java-union.egl"
	target: outDir+union.identifier+extension
}

rule Constants {
	guard: Const.allInstances().size() > 0
	parameters : Map {
		"constants" = Const.allInstances(),
		"className" = constsName,
		"package" = pkgId,
		"author" = author
	}
	template: "thrift-java-constants.egl"
	target: outDir+constsName+extension
}

rule Services transform service : Service {
	parameters : Map {
		"service" = service,
		"identifier" = service.identifier,
		"supers" = service.supers,
		"functions" = service.functions,
		"structEGL" = structEGL,
		"outputFile" = outDir+service.identifier+extension,
		"package" = pkgId,
		"comment" = service.comment,
		"date" = date,
		"author" = author
	}
	template: "thrift-java-service.egl"
	target: outDir+service.identifier+extension
}
